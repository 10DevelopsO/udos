
== Building Packages ==

This page provides information regarding the package building process. The first section documents building a package with the low level command <tt>package</tt>. The second section refers to building packages with the <tt>haikuporter</tt> tool.


=== Building a Package with the "package" Command ===

The package file format is specified in detail in a [[PackageManagement/FileFormat| separate document]]. This section presents information from the perspective of how to build a package file with the <tt>package</tt> command.

An hpkg file is an archive file (just like tar or zip files) that additionally contains package meta information in a separate section of the file. When building an hpkg file via the <tt>package</tt> command the meta information must be provided via a <tt>.PackageInfo</tt> file. For convenience, the file itself is added to the archive as well and can be extracted later, but it will be ignored by packagefs.

The <tt>.PackageInfo</tt> file must be located in the top directory that is archived. A <tt>package</tt> invocation usually looks like that:
<pre>
package create -C foo-4.5.26-1 foo-4.5.26-1-x86.hpkg
</pre>
or (packaging a gcc2 build from within the folder):
<pre>
cd foo-4.5.26-1
package create ../foo-4.5.26-1-x86_gcc2.hpkg
</pre>

The argument of the <tt>-C</tt> option specifies the directory whose contents to archive (by default the current directory), the remaining argument is the path of the package file to be built.

==== The .PackageInfo ====

The contents of the <tt>.PackageInfo</tt> adheres to a restricted driver settings syntax. It consists of name-value pairs, following this simple grammar:
<pre>
package_info	::= attribute*
attribute	::= name value_list
value_list	::= value | ( "{" value* "}" )
value		::= value_item+ ( '\n' | ';' )
</pre>

<tt>name</tt> can be one of the attribute names defined below. <tt>value_item</tt> is either an unquoted string not containing any whitespace characters or a string enclosed in quotation marks (<tt>"</tt> or <tt>'</tt>) which can contain whitespace and also escaped characters (using <tt>\</tt>).

The supported attributes are:
*<tt>name</tt>: The name of the package, not including the package version. Must only contain <tt>entity_name_char</tt> characters.
<pre>
entity_name_char	::= any character but '-', '/', '=', '!', '<', '>', or whitespace
</pre>
*<tt>version</tt>: The version of the package. The string must have the <tt>version</tt> format (see the [[PackageManagement/BuildingPackages#VersionStrings| Version Strings]] section).
*<tt>architecture</tt>: The system architecture the package has been built for. Can be either of:
   - <tt>any</tt>: Any architecture (e.g. a documentation package).
   - <tt>x86</tt>: Haiku x86, built with gcc 4.
   - <tt>x86_gcc2</tt>: Haiku x86, built with gcc 2.
*<tt>summary</tt>: A short (one-line) description of the package.
*<tt>description</tt>: A longer description of the package.
*<tt>vendor</tt>: The name of the person/organization publishing this package.
*<tt>packager</tt>: The name and e-mail address of person that created this package (e.g. "Peter Packman <peter.packman@example.com>").
*<tt>copyrights</tt>: A list of copyrights applying to the software contained in this package.
*<tt>licenses</tt>: A list of names of the licenses applying to the software contained in this package.
*<tt>urls</tt>: A list of URLs referring to the packaged software's project home page. The list elements can be regular URLs or email-like named URLs (e.g. "Project Foo <http://foo.example.com>").
*<tt>source-urls</tt>: A list of URLs referring to the packaged software's source code or build instructions. Elements have the same format as those of <tt>urls</tt>.
*<tt>flags</tt>: A list of boolean flags applying to the package. Can contain any of the following:
   - <tt>approve_license</tt>: This package's license requires approval (i.e. must be shown to and acknowledged by user before installation).
   - <tt>system_package</tt>: This is a system package (i.e. lives under "/boot/system") .
*<tt>provides</tt>: A list of entities provided by this package. The list elements must have the following format:
<pre>
entity		::= entity_name [ "=" version_ref ] [ ( "compat" | "compatible" ) ">=" version_ref ]
entity_name	::= [ entity_type ":" ] entity_name_char+
entity_type	::= "lib" | "cmd" | "app" | "add_on"
</pre>
   See the [[PackageManagement/BuildingPackages#VersionStrings| Version Strings]] section for the <tt>version_ref</tt> definition.
   The first <tt>version_ref</tt> specifies the version of the provided entity. It can be omitted e.g. for abstract resolvables like "web_browser". The <tt>version_ref</tt> after the "compat"/"compatible" string specifies the oldest version the resolvable is backwards compatible with.
   The <tt>entity_type</tt> specifies the type of entity provided: a library ("lib"), a command line program ("cmd"), an application ("app"), or an add-on ("add-on").
*<tt>requires</tt>: A list of entities required by this package. The list elements must have the following format:
<pre>
required_entity		::= entity_name [ version_operator version_ref [ "base" ] ]
version_operator	::= "<" | "<=" | "==" | "!=" | ">=" | ">"
</pre>
   See the [[PackageManagement/BuildingPackages#VersionStrings| Version Strings]] section for the <tt>version_ref</tt> definition. If "base" is specified, the specified entity is the base package for this package. The package manager shall ensure that this package is installed in the same installation location as its base package.
*<tt>supplements</tt>: A list of entities that are supplemented by this package (i.e. this package will automatically be selected for installation if the supplemented entities are already installed). The list elements must have the <tt>required_entity</tt> format.
*<tt>conflicts</tt>: A list of entities that this package conflicts with (i.e. only one of both can be installed at any time). The list elements must have the <tt>required_entity</tt> format.
*<tt>freshens</tt>: A list of entities that are being freshened by this package (i.e. this package will patch one or more files of the package(s) that provide this entity). The list elements must have the <tt>required_entity</tt> format.
*<tt>replaces</tt>: A list of entities that are being replaced by this package (used if the name of a package changes, or if a package has been split). The list elements must have the <tt>entity_name</tt> format.
*<tt>global-writable-files</tt>: A list of global writable file infos. The list elements must have the following format:
<pre>
global_writable_file_info	::= path [ "directory" ] [ "keep-old" | "manual" | "auto-merge" ]
</pre>
   <tt>path</tt> is the relative path of the writable file or directory, starting with "settings/" or any other writable directory. If the "directory" keyword is given, the path refers to a directory. If no other keyword is given after the path respectively after the "directory" keyword, the file or directory is not included in the package. It will be created by the software or by the user. If a keyword is given, the file or directory (a default version) is included in the package and it will be extracted on package activation. The keyword specifies what shall happen when the package is updated and a previous default version of the file or directory has been modified by the user:
   - "keep-old": Indicates that the software can read old files and the user-modified file or directory should be kept.
   - "manual": Indicates that the software may not be able to read an older file and the user may have to manually adjust it.
   - "auto-merge": Indicates that the file format is simple text and a three-way merge shall be attempted (not applicable for directories).
*<tt>user-settings-files</tt>: A list of user settings file infos. The list elements must have the following format:
<pre>
user_settings_file_info	::= path [ "directory" | "template" template_path ]
</pre>
   <tt>path</tt> is the relative path of the settings file or directory, starting with "settings/". It is not included in the package. However, if <tt>template_path</tt> is specified, it is a path to a file included in the package that can serve as a template for the settings file. It doesn't imply any automatic action on package activation, though. If the "directory" keyword is given, the path refers to a settings directory (typical when a program creates multiple settings files).
*<tt>users</tt>: A list of specifications for Unix users the packaged software requires. The list elements must have the following format:
<pre>
user:	::= name [ "real-name" real_name ] "home" home_path [ "shell" shell_path ] [ "groups" group+ ]
</pre>
   <tt>name</tt> is the name of the Unix user, <tt>real_name</tt>, if specified, the real name of the user, <tt>home_path</tt> the path to the user's home directory, <tt>shell_path</tt> the path to the user's shell, and <tt>group</tt> the name of a Unix group the users is a member of. If the respective components are not specified, <tt>name</tt> is also used as the user's real name, "/bin/bash" is the path of the user's shell, and the user will belong to the default user group.
*<tt>groups</tt>: A list of names of Unix groups the packaged software requires.
*<tt>post-install-scripts</tt>: A list of paths of files included in the package, which shall be executed on package activation. Each path must start with "boot/post-install/".

==== Version Strings ====

Versions strings are used in three contexts: For the package version, for resolvable versions (<tt>provides</tt>), and in dependency version expressions (<tt>requires</tt>, <tt>supplements</tt>, <tt>conflicts</tt>, <tt>freshens</tt>). They are structurally identical, with the exception that the former requires a revision component (<tt>version</tt>), while the latter two don't (<tt>version_ref</tt>):
<pre>
version		::= major [ "." minor [ "." micro ] ] [ "~" pre_release ] "-" revision
version_ref	::= major [ "." minor [ "." micro ] ] [ "~" pre_release ] [ "-" revision ]
major		::= alphanum_underline+
minor		::= alphanum_underline+
micro		::= alphanum_underline_dot+
pre_release	::= alphanum_underline_dot+
revision	::= positive_non_zero_integer
</pre>
The meaning of the major, minor, and micro version parts is vendor specific. A typical, but not universal (!), convention is to increment the major version when breaking binary compatibility (i.e. version a.d.e is backwards compatible to version a.b.c for all b.c <= d.e), to increment the minor version when adding new features (in a binary compatible way), and to increment the micro version for bug fix releases. There are, however, projects that use different conventions which don't imply that e.g. version 1.4 is backwards compatible with version 1.2. Which convention is used is important for the packager to know, as it is required for a correct declaration of the compatibility versions for the provided resolvables. The compatibility version specifies the oldest version the provided resolvable is backwards compatible with, thus implying the version range requested by a dependent package the resolvable can satisfy. When following the aforementioned convention a resolvable of version 2.4.3 should have compatibility version 2 (or, semantically virtually identical, 2.0.0). Not following the convention 2.4 may be correct instead. If no compatibility version is specified, the resolvable can only satisfy dependency constraints with an exactly matching version.

The pre-release part of the version string has special semantics for comparison. Unlike minor and micro its presence makes the version older. E.g. version R1.0~alpha1 is considered to be older than version R1.0. When both version strings have a pre-release part, that part is compared naturally after the micro part (R1.0.1~alpha1 > R1.0 > R1.0~beta1 > R1.0~alpha2).

The revision part of the version string is assigned by the packager (not by the vendor). It allows to uniquely identify updated packages of the same vendor version of a software.

==== Package File Names ====

A package file name should have the following form:
<pre>
file_name	::= name "-" version "-" architecture ".hpkg"
</pre>

==== Example package file ====
<pre>
name			example
version			42.17-12
architecture		x86_gcc2
summary			"This is an example package file"
description		"Haiku has a very powerful package management system. Really, you should try it!
it even supports muliline strings in package descriptions"
packager		"John Doe <test@example.com>"
vendor			"Haiku Project"
licenses {
	"MIT"
}
copyrights {
	"Copyright (C) 1812-2013 by John Doe <test@example.com>"
}
provides {
        example = 42.17-12
	cmd:example = 3.1
}
requires {
	haiku >= r1~alpha4_pm_hrev46213-1
	lib:libpython2.6 >= 1.0
}
urls {
	"http://example.com/"
}
global-writable-files {
	"settings/example/configurationFile" keep-old
	"settings/example/servers" directory keep-old
}
source-urls {
	"Download <http://example.com/source.zip>"
}
</pre>


=== Building a Package with "haikuporter" ===

<tt>haikuporter</tt> is a high level tool for building packages. As input it reads a build recipe file for a certain version of a software (aka port) and produces one or more packages, as declared in the recipe. A recipe specifies package requirements similar to how it is done in a <tt>.PackageInfo</tt> file. When asked to build a port, <tt>haikuporter</tt> resolves the respective dependencies and recursively builds all not-yet-built ports required for the requested port. <tt>haikuporter</tt> itself and a large library of recipe files are hosted at [https://github.com/haikuports/ HaikuPorts]. A detailed [https://github.com/haikuports/haikuports/wiki/HaikuPorterForPM documentation for haikuporter] and the [https://github.com/haikuports/haikuports/wiki/HaikuPorter-BuildRecipes recipe format] can also be found there.
